; フィボナッチ数列の最初の20項を出力する
FIBO     START  ENTRY
C1       DC     1           ; 定数1
COUNT    DC     20          ; ループ回数
; 処理開始
ENTRY    LD     GR0,C1      ; GR0=現在の項　初期値=1
         XOR    GR1,GR1     ; GR1=前の項　初期値=0
         LD     GR2,COUNT   ; GR2=カウンタ
; 現在の項を出力
LOOP     CALL   OUTDEC      ; GR0=現在の項の値を出力
; 次の項の値を計算
         LD     GR3,GR1     ; 前の項を退避
         LD     GR1,GR0     ; 次回の前の項=現在の項
         ADDL   GR0,GR3     ; 前の項（退避）を加える
         SUBL   GR2,C1      ; カウンタデクリメント
         JNZ    LOOP        ; 20回ループ
         RET
         END

OUTDEC   START  ENTRY
BUF      DS     5
LEN      DC     5
ENTRY    PUSH   0,GR2
         PUSH   0,GR1
         LD     GR2,GR0
         PUSH   0,GR2
         LAD    GR1,BUF
         CALL   BINDEC
         OUT    BUF,LEN
         POP    GR0
         POP    GR1
         POP    GR2
         RET
;
; BINDEC 符号無しバイナリ→10進文字列
; 入力  GR0=値
;       GR1=5語のバッファ
; 出力  5語のバッファに文字がうまる（右詰、左はスペース）
;       GR0とGR1は不定になる
;
BINDEC   PUSH   0,GR2
; バッファの初期化
         LAD    GR2,#0020   ; スペースの文字コード
         ST     GR2,0,GR1   ; バッファをスペースクリア
         ST     GR2,1,GR1
         ST     GR2,2,GR1
         ST     GR2,3,GR1
         LAD    GR2,4,GR1   ; GR2=バッファの最後の文字のアドレス
; 10で割った余りをストアしていく
BDLOOP   DS     0
         LAD    GR1,10      ; 10で割る
         CALL   DIVL        ; GR1=商、GR0=余り
         ADDL   GR0,='0'    ; 数字に変換
         ST     GR0,0,GR2   ; 変換した文字を格納
         LAD    GR2,-1,GR2  ; バッファのアドレス
         LD     GR0,GR1     ; 商を次回の被除数にする
         JNZ    BDLOOP      ; 0なら終わり
; 終わり。
         POP    GR2
         RET
;
; DIVL 符号無し除算ルーチン
; 入力  GR0=被除数
;       GR1=除数
; 出力　GR0=剰余
;       GR1=商
; 条件  除数は0でないこと。
;
DIVL     PUSH   0,GR2          ; レジスタ待避
         PUSH   0,GR3
;
         LAD    GR2,1          ; マスク
         SUBL   GR3,GR3        ; 商
;
; 開始位置を求める
;
FINDENT  CPL    GR0,GR1        ; 引けないに決まっているなら
         JZE    DVLOOP         ; 空回りしても無駄
         JMI    DVLOOP         ;
;
         SLL    GR2,1          ; マスクを１ビットずらす
         SLL    GR1,1          ; 除数を１ビットずらす
         JPL    FINDENT        ; 最上位ビットが立ったらこれ以上無理
;
; 除算のメインループ。１ビットずつずらしながら引いていく
;
DVLOOP   CPL    GR0,GR1        ; 引ける？
         JMI    DONTSET        ; 引けないならスキップ
;
         OR     GR3,GR2        ; 商のビットを立てる
         SUBL   GR0,GR1        ; 被除数から引く
         JZE    LOOPEND        ; 割り切れた
;
DONTSET  SRL    GR1,1          ; 除数
         SRL    GR2,1          ; マスク
         JNZ    DVLOOP         ; 全部のビットが終わった？
;
; 除算終了。GR3に入っている商を戻す
;
LOOPEND  LD     GR1,GR3
         POP    GR3
         POP    GR2
         RET
         END